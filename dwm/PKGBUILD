# Maintainer: James Griffis <setkeh@gmail.com>

pkgname=dwm
pkgver=6.2
pkgrel=3
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'dmenu-git')
install=dwm.install
source=(http://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
	config.def.h
	config.mk
  dwm.desktop)

prepare() {
  cd $srcdir/$pkgname-$pkgver
  cp $srcdir/config.def.h config.def.h
  cp $srcdir/config.mk config.mk
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
  install -m644 -D $srcdir/dwm.desktop $pkgdir/usr/share/xsessions/dwm.desktop
}

md5sums=('9929845ccdec4d2cc191f16210dd7f3d'
         '8dafaee73f7b48c6489885df6ac42d8a'
         'd61cf5e985e55bcb341977b5181ee24d'
         'f3b789acf1550e7d6de1e9f956c6e8ec')
